sourceSets {
    endtoendTest {
        kotlin.srcDir 'src/endtoend-test/kotlin'
        resources.srcDir 'src/endtoend-test/resources'
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    endtoendTestCompile('com.jcraft:jsch:0.1.55')

    endtoendTestCompile sourceSets.main.output
    endtoendTestCompile sourceSets.test.output
    endtoendTestCompile configurations.compile
    endtoendTestCompile configurations.testCompile
    endtoendTestRuntime configurations.runtime
    endtoendTestRuntime configurations.testRuntime
    endtoendTestRuntime configurations.testImplementation
}

task endtoendTest(type: Test) {
    useJUnitPlatform()
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the endtoend tests.'

    testClassesDirs = sourceSets.endtoendTest.output.classesDirs
    classpath = sourceSets.endtoendTest.runtimeClasspath

    binResultsDir = file("$buildDir/endtoend-test-results/binary/endtoendTest")

    systemProperties project.properties.subMap(["engine.connection", "s3.location"])

    reports {
        html.destination = file("$buildDir/reports/endtoend-test")
        junitXml.destination = file("$buildDir/endtoend-test-results")
    }

    testLogging {
        events "FAILED", "PASSED", "SKIPPED"
        exceptionFormat "FULL"
        showExceptions true
        showCauses true
        showStackTraces true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
    outputs.upToDateWhen { false }

    mustRunAfter tasks.test
    mustRunAfter tasks.integrationTest
}
